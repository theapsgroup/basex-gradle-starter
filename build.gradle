plugins {
  id "com.github.johnrengelman.shadow" version "5.0.0"
  id "java"
}

repositories {
    maven { url "http://files.basex.org/maven" }
    maven { url "http://xqj.net/maven" }
    mavenCentral()
}

jar {
   manifest {
       attributes 'Main-Class': 'org.basex.BaseXHTTP'
   }
}

def jettyVersion = '8.1.18.v20150929'

dependencies {
    runtime "org.basex:basex-api:8.5"
    runtime "org.eclipse.jetty:jetty-webapp:${jettyVersion}"
    runtime "jp.sourceforge.igo:igo:0.4.3"
    runtime "org.apache:lucene-stemmers:3.4.0"
    runtime "org.ccil.cowan.tagsoup:tagsoup:1.2.1"
    runtime "xml-resolver:xml-resolver:1.2"
    runtime("com.thaiopensource:jing:20091111") {
        exclude group: 'xerces', module: 'xercesImpl'
        exclude group: 'xml-apis', module: 'xml-apis'
        exclude group: 'net.sf.saxon', module: 'saxon'
        exclude group: 'isorelax', module: 'isorelax'
    }
    runtime "net.sf.saxon:Saxon-HE:9.9.1-2"
}

task http(type: JavaExec) {
    description "BaseX HTTP server"
    group "XML"
    classpath configurations.runtime
    standardInput = System.in
    main = "org.basex.BaseXHTTP"
}

task client(type: JavaExec) {
    description "BaseX console"
    group "XML"
    classpath configurations.runtime
    standardInput = System.in
    main = "org.basex.BaseXClient"
}

task gui {
    description "BaseX GUI"
    group "XML"
    doLast {
        ant.java(
            fork: true, 
            spawn: true, 
            classpath: configurations.runtime.asPath, 
            classname: "org.basex.BaseXGUI",
            clonevm: true)
    }
}
